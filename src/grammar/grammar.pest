//
// Grammar specification for ledger files as interpreted by dinero rs
// Test in https:://pest.rs
//

journal = { SOI ~ (directive | blank_line | transaction | automated_transaction | journal_comment )* ~ ws* ~EOI}
blank_line = {ws* ~ NEWLINE }
directives = {directive* ~ EOI}
journal_comment = {(";" | "!" | "#") ~ (!end ~ ANY)* ~ end}

// Directives
directive = { include | price
    | tag_dir 
    | account_dir 
    | commodity 
    | payee_dir 
    }

include = { "include" ~ws+ ~ glob   ~ws*~end}
glob = { string | (!end ~ ANY)* }
price = {"P" ~ ws* ~ date ~ (ws ~ time)? ~ ws+ ~ commodity_in_directive ~ ws* ~number ~ ws* ~ commodity_in_directive ~ws* ~ comment? ~ end}
commodity = { "commodity" ~ ws+ ~ commodity_in_directive ~ ws* ~ comment? ~ end ~
    (sep ~ 
        ( 
            comment 
            | (commodity_property ) 
            | (flag ~ ws* ~ comment?)
        )?
    ~end)*
    }
commodity_in_directive = { string | unquoted }
payee_dir = { "payee" ~ ws+ ~ payee ~ ws* ~ comment? ~end ~ 
    (sep ~ 
        ( 
            comment 
            | (payee_property) 
        )? 
    ~end)*
    }
tag_dir = { "tag" ~ ws+ ~ tag ~ ws* ~ comment? ~end ~ 
    (sep ~ 
        ( 
            comment 
            | (tag_property) 
        )? 
    ~end)*
    }
account_dir = { "account" ~ ws+ ~ account ~ ws* ~ comment? ~ end ~
    (sep ~ 
        ( 
            comment 
            | (account_property ) 
            | (flag ~ ws* ~ comment?)
        )?
    ~end)*
    }
commodity_property = { (alias | note | format ) ~ ws+ ~ property_value }
payee_property = { (alias | note) ~ ws+ ~ property_value }
account_property = { (alias | payee_subdirective | check | assert | note | isin ) ~ ws+ ~ property_value }
tag_property = { (check | assert) ~ ws+ ~ property_value }
property_value = { string | (!end ~ ANY )*}
alias = { "alias" }
note = { "note" }
format = { "format" }
check = { "check" }
assert = { "assert" }
isin = {"isin"}
payee_subdirective = {"payee"}
flag = { default }
default = {"default"}
// A transaction
transaction_head = {
    transaction_date ~              // date
    ("=" ~ effective_date)? ~       // effective_date
    (ws+ ~ status)? ~               // status
    ws* ~ code?                     // code
    ~ ws* ~ description             // description
    ~ ws* ~ (("|"|"@") ~ ws* ~payee)?     // payee
    ~ws* ~ comment? }    
automated_transaction_head = {
    "="  ~ ws* ~ automated_description     // description
    ~ws* ~ comment? }                      // comment
transaction = {transaction_head
    ~ NEWLINE
    ~ (sep ~ comment ~ end)*
    ~ posting+ }
automated_transaction = {automated_transaction_head
    ~ NEWLINE
    ~ (sep ~ comment ~ end)*
    ~ (posting|automated_posting)* }
transaction_date = {date ~ (ws ~ time)?}
effective_date = {date ~ (ws ~ time)?}
code = { "(" ~ string ~ ")" }
status = { "*"| "!" }
quote = _{"\"" | "'"}
payee = { string | (!"|" ~ !";" ~!end ~ ANY)* }
tag = { string | (!"|" ~ !";" ~!end ~ ANY)* }
description = { string | (!"|" ~ !";" ~!end ~ ANY)* }
automated_description = { string | (!";" ~!end ~ ANY)* }
comment = {";" ~ (!end ~ ANY)* ~ ws*}

posting = { sep ~ status? ~ 
	posting_kind ~ 
    ((sep ~ ws* ~ (amount ~ ws*) ~ (cost ~ ws*)?)? ~
    ws* ~balance? ~ ws* ~ comment? ~ end )
    ~ (sep ~ comment ~ end)*
    }
automated_posting = { sep ~ status? ~ 
	posting_kind ~ 
    (sep ~ ws* ~ (value_expr | number) ~ ws* ~ comment? ~ end )
    ~ (sep ~ comment ~ end)*
    }
amount = {money}
cost = { ("@@" | "@") ~ ws* ~ money }
balance = {"=" ~ ws* ~ money ~ ws*}
posting_kind = {  virtual_no_balance | virtual_balance | real}
real = { account }
virtual_no_balance = { "(" ~ account ~ ")" }
virtual_balance = { "[" ~ account ~ "]" }
account = { string | 
    ((unquoted ~ ( (" - "|" = " ~ " & "|" ") ~ unquoted)*)) ~ (":" ~ (unquoted ~ ( (" - "|" = " ~ " & "|" ") ~ unquoted)*))* }
// Dates
date = { year ~ date_sep ~ month ~ date_sep ~ day }
time = { hour ~ ":" ~ minute ~ (":" ~ second) }
datetime = { date ~ (ws ~ time)? }

date_sep = { "." | "/" | "-"}
year = { "-"? ~ bigint}
month = {("1" ~ ("0" | "1" | "2")) | ("0"? ~ ASCII_NONZERO_DIGIT)}
day = {("3" ~ ("0" | "1")) |
	(("1" | "2") ~ ASCII_DIGIT ) |
    ("0"? ~ ASCII_NONZERO_DIGIT) }
hour = { (("0"|"1") ~ ASCII_DIGIT) | ("2" ~ ("0" ~"1" ~"2" ~"3"))}
minute = { ("0"|"1"|"2"|"3"|"4"|"5") ~ ASCII_DIGIT }
second = { ("0"|"1"|"2"|"3"|"4"|"5") ~ ASCII_DIGIT ~ ("." ~ bigint)? }


// Grammar specification for value expressions

// A value expression is an expression between parenthesis
value_expr = {"(" ~ ws* ~ expr ~ ws* ~ ")"}

// Then the expression builds up in terms of increasing preference
expr = { or_expr }
or_expr = { and_expr ~ ws* ~ ( or ~ ws* ~ and_expr ) * }
and_expr = { comparison_expr ~ ws* ~ ( and ~ ws* ~ comparison_expr ) * }
comparison_expr = { additive_expr ~ ws* ~ ( comparison ~ ws* ~ additive_expr ) * }
additive_expr = { multiplicative_expr ~ ws* ~ ( add ~ ws* ~ multiplicative_expr ) * }
multiplicative_expr = { primary ~ ws* ~ ( mult ~ ws* ~ primary )* }
primary = {
    ("(" ~ ws* ~ expr ~ ws* ~ ")") |
    (unary ~ ws* ~ expr) |
    term |
    (function ~ ws* ~ "(" ~ ws* ~ expr ~ ws* ~ ("," ~ ws* ~ expr ~ ws*)* ~ ")")
    }

term = _{ variable | money | number | regex | string }
money = { (number ~ ws* ~ currency) | (currency ~ ws* ~ number) | ("0" ~ &(ws | sep | end ))}
currency = { string | unquoted_no_number }
regex = { "/" ~ (!"/" ~ ANY)* ~ "/"}
string = {
	("\"" ~ (("\\\"") | (!"\"" ~ ANY))* ~ "\"") |
	("'" ~ (("\\'") | (!"'" ~ ANY))* ~ "'")
    }
reserved = _{ "\n" | "\t" | "+" | "*" | "/" | "\\" | "|" | "%" | "<" | ">" | ":" | "?" | "(" | ")" | ";" | "[" | "]" }
unquoted = { !reserved ~ !"=" ~ !"-" ~ !"&" ~
    (!reserved ~ !SEPARATOR ~ ANY)+ }
currency_parts = _{ !reserved ~ !"=" ~ !"-" ~ !"&" ~
    (!reserved ~ !SEPARATOR ~ !ASCII_DIGIT ~ !"-" ~ ANY)+ }
unquoted_no_number = {currency_parts ~ ("-" ~ currency_parts)*}
variable = {
    "account" |
    "payee" |
    "date" |
    "note" |
    "amount" |
    "total_amount" |
    "cost" |
    "value" |
    "gain" |
    "depth" |
    "posting_number" |
    "posting_count" |
    "cleared" |
    "real" |
    "not_automated" |
    "running_total" |
    "note" |
    // Abbreviations go later
    "T" | "N" | "O" | "Z" | "R" | "X" |
    "n" | "l" | "g" | "v" | "b"
}


// helpers
number = { "-"? ~ bigint ~ ("." ~ bigint)? }
bigint = _{ ASCII_DIGIT+ }
ws = _{ " " | "\t" }
sep = _{("\t" | " \t" | "  ") ~ SEPARATOR* }
end = _{ EOI | NEWLINE | blank_line}


add = { "+" | "-" }
mult = { "*" | "/" }
and = {"&" | "and"}
or = {"|" | "or" }
unary = { "-" | "!" | "not" }
function = { "abs" | "has_tag" | "to_date" | "any" | "tag" }
comparison = { eq | ne | ge | gt | le | lt }
eq = { "=~" | "=="}
ne = { "!=" }
gt = { ">" }
ge = { ">=" }
le = { "<=" }
lt = { "<" }