//
// Grammar specification for ledger files as interpreted by dinero rs
// Test in https:://pest.rs
//

journal = { SOI ~ (directive | blank_line | transaction | journal_comment )+ ~ EOI}
blank_line = {ws* ~ NEWLINE }
directives = {directive* ~ EOI}
journal_comment = {(";" | "!" | "#") ~ (!end ~ ANY)* ~ end}

// Directives
directive = { include | price
    // tag 
    | account_dir 
    | commodity 
    | payee_dir 
    }

include = { "include" ~ws+ ~ (string | (!end ~ ANY)*)   ~ws*~end}
price = {"P" ~ ws* ~ date ~ (ws ~ time)? ~ ws+ ~ currency ~ ws* ~number ~ ws* ~ currency ~ws* ~ comment? ~ end}
commodity = { "commodity" ~ ws+ ~ currency ~ ws* ~ comment? ~ end ~
    (sep ~ 
        ( 
            comment 
            | (commodity_property ) 
            | (flag ~ ws* ~ comment?)
        )?
    ~end)*
    }
payee_dir = { "payee" ~ ws+ ~ payee ~ ws* ~ comment? ~end ~ 
    (sep ~ 
        ( 
            comment 
            | (payee_property) 
        )? 
    ~end)*
    }
account_dir = { "account" ~ ws+ ~ account ~ ws* ~ comment? ~ end ~
    (sep ~ 
        ( 
            comment 
            | (account_property ~ ws+ ~ (string | ( !";" ~ !end ~ ANY )*) ~ ws* ~ comment?) 
            | (flag ~ ws* ~ comment?)
        )?
    ~end)*
    }
commodity_property = { (alias | note | format ) ~ ws+ ~ property_value }
payee_property = { (alias | note) ~ ws+ ~ property_value }
property_value = { string | (!end ~ ANY )*}
alias = { "alias" }
note = { "note" }
format = { "format" }
flag = { default }
default = {"default"}
account_property = { "alias" | "payee" }
// A transaction
transaction_head = {
    transaction_date ~           // date
    ("=" ~ effective_date)? ~  // effective_date
    ws+ ~ status? ~                 // status
    ws* ~ code?                     // code
    ~ ws* ~ description             // description
    ~ ws* ~ ("|" ~ ws* ~payee)?     // payee
    ~ws* ~ comment? }               // comment
transaction = {transaction_head
    ~ NEWLINE
    ~ (sep ~ comment ~ end)*
    ~ posting+ }
transaction_date = {date ~ (ws ~ time)?}
effective_date = {date ~ (ws ~ time)?}
code = { "(" ~ string ~ ")" }
status = { "*"| "!" }
quote = _{"\"" | "'"}
payee = { string | (!"|" ~ !";" ~!end ~ ANY)* }
description = { string | (!"|" ~ !";" ~!end ~ ANY)* }
comment = {";" ~ (!end ~ ANY)*}

posting = {  sep ~ status? ~ 
	posting_kind ~ 
    ((sep ~ ws* ~ (amount ~ ws*) ~ (cost ~ ws*)?)? ~
    balance? ~ ws* ~ comment? ~ end )
    ~ (sep ~ comment ~ end)*
    }
amount = {money}
cost = { ("@@" | "@") ~ ws* ~ money }
balance = {"=" ~ ws* ~ money ~ ws*}
posting_kind = {  virtual_no_balance | virtual_balance | real}
real = { account }
virtual_no_balance = { "(" ~ account ~ ")" }
virtual_balance = { "[" ~ account ~ "]" }
account = { string | ((unquoted ~ (" " ~ unquoted)*)) ~ (":" ~ (unquoted ~ (" " ~ unquoted)*))* }
// Dates
date = { year ~ date_sep ~ month ~ date_sep ~ day }
time = { hour ~ ":" ~ minute ~ (":" ~ second) }
datetime = { date ~ (ws ~ time)? }

date_sep = { "." | "/" | "-"}
year = { "-"? ~ bigint}
month = {("1" ~ ("0" | "1" | "2")) | ("0"? ~ ASCII_NONZERO_DIGIT)}
day = {("3" ~ ("0" | "1")) |
	(("1" | "2") ~ ASCII_DIGIT ) |
    ("0"? ~ ASCII_NONZERO_DIGIT) }
hour = { (("0"|"1") ~ ASCII_DIGIT) | ("2" ~ ("0" ~"1" ~"2" ~"3"))}
minute = { ("0"|"1"|"2"|"3"|"4"|"5") ~ ASCII_DIGIT }
second = { ("0"|"1"|"2"|"3"|"4"|"5") ~ ASCII_DIGIT ~ ("." ~ bigint)? }


// Grammar specification for value expressions

// A value expression is an expression between parenthesis
value_expr = {"(" ~ ws* ~ expr ~ ws* ~ ")"}

// Then the expression builds up in terms of increasing preference
expr = { or_expr }
or_expr = { and_expr ~ ws* ~ ( or ~ ws* ~ and_expr ) * }
and_expr = { comparison_expr ~ ws* ~ ( and ~ ws* ~ comparison_expr ) * }
comparison_expr = { additive_expr ~ ws* ~ ( comparison ~ ws* ~ additive_expr ) * }
additive_expr = { multiplicative_expr ~ ws* ~ ( add ~ ws* ~ multiplicative_expr ) * }
multiplicative_expr = { primary ~ ws* ~ ( mult ~ ws* ~ primary )* }
primary = {
    ("(" ~ ws* ~ expr ~ ws* ~ ")") |
    (unary ~ ws* ~ expr) |
    term |
    (function ~ ws* ~ "(" ~ ws* ~ expr ~ ws* ~ ("," ~ ws* ~ expr ~ ws*)* ~ ")")
    }

term = _{ variable | money | number | regex | string }
money = { (number ~ ws* ~ currency) | (currency ~ ws* ~ number) }
currency = { string | unquoted_no_number }
regex = { "/" ~ (!"/" ~ ANY)* ~ "/"}
string = {
	("\"" ~ (("\\\"") | (!"\"" ~ ANY))* ~ "\"") |
	("'" ~ (("\\'") | (!"'" ~ ANY))* ~ "'")
    }
reserved = _{ "\n" | "+" | "*" | "/" | "\\" | "|" | "%" | "<" | ">" | ":" | "?" |  "(" |  ")" | ";"}
unquoted = { !reserved ~ !"=" ~ !"-" ~ !"&" ~
    (!reserved ~ !SEPARATOR ~ ANY)+ }
currency_parts = _{ !reserved ~ !"=" ~ !"-" ~ !"&" ~
    (!reserved ~ !SEPARATOR ~ !ASCII_DIGIT ~ !"-" ~ ANY)+ }
unquoted_no_number = {currency_parts ~ ("-" ~ currency_parts)*}
variable = {
    "account" |
    "payee" |
    "date" |
    "note" |
    "amount" |
    "total_amount" |
    "cost" |
    "value" |
    "gain" |
    "depth" |
    "posting_number" |
    "posting_count" |
    "cleared" |
    "real" |
    "not_automated" |
    "running_total" |
    "note" |
    // Abbreviations go later
    "T" | "N" | "O" | "Z" | "R" | "X" |
    "n" | "l" | "g" | "v" | "b"
}


// helpers
number = { "-"? ~ bigint ~ ("." ~ bigint)? }
bigint = _{ ASCII_DIGIT+ }
ws = _{" "}
sep = _{("\t" | " \t" | "  ") ~ SEPARATOR* }
end = _{ EOI | NEWLINE }


add = { "+" | "-" }
mult = { "*" | "/" }
and = {"&" | "and"}
or = {"|" | "or" }
unary = { "-" | "!" | "not" }
function = { "abs" | "has_tag" | "to_date" | "any" | "tag" }
comparison = { eq | ne | ge | gt | le | lt }
eq = { "=~" | "=="}
ne = { "!=" }
gt = { ">" }
ge = { ">=" }
le = { "<=" }
lt = { "<" }